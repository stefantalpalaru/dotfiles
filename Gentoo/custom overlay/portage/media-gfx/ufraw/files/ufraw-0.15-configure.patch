--- configure.ac.orig	2009-02-11 21:33:33.000000000 +0100
+++ configure.ac	2009-02-14 22:52:35.000000000 +0100
@@ -83,13 +83,27 @@
 PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.6)
 PKG_CHECK_MODULES(GLIB, glib-2.0)
 PKG_CHECK_MODULES(LCMS, lcms)
-PKG_CHECK_MODULES(GTKIMAGEVIEW, gtkimageview >= 1.3,
-  [ have_gtkimageview=yes
-    GTK_LIBS=$GTKIMAGEVIEW_LIBS
-    GTK_CFLAGS=$GTKIMAGEVIEW_CFLAGS
-    AC_DEFINE(HAVE_GTKIMAGEVIEW, 1, have gtk image view) ],
-  [ have_gtkimageview=no
-    AC_MSG_RESULT($GTKIMAGEVIEW_PKG_ERRORS) ] )
+
+AC_ARG_WITH([gtkimageview],
+  [AS_HELP_STRING([--with-gtkimageview],
+  [use gtkimageview for previews @<:@default=check@:>@])],
+  [],
+  [with_gtkimageview=check])
+
+have_gtkimageview=no
+AS_IF([test "x$with_gtkimageview" != xno],
+  [PKG_CHECK_MODULES(GTKIMAGEVIEW, gtkimageview >= 1.3,
+    [ have_gtkimageview=yes
+      GTK_LIBS=$GTKIMAGEVIEW_LIBS
+      GTK_CFLAGS=$GTKIMAGEVIEW_CFLAGS
+      AC_DEFINE(HAVE_GTKIMAGEVIEW, 1, have gtk image view) ],
+    [ have_gtkimageview=no
+      if test "x$with_gtkimageview" != xcheck; then
+        AC_MSG_FAILURE(
+          [--with-gtkimageview was given, but test for gtkimageview failed])
+      fi
+    ] ) ] )
+
 AC_SUBST(GTK_LIBS)
 if test "$have_gtkimageview" = "yes"; then
   PKG_CHECK_MODULES(gtk_image_view_damage_pixels, gtkimageview > 1.5.0,
@@ -106,12 +120,25 @@
 test $prefix = NONE || pkg_prefix="$pkg_prefix --define-variable=prefix=$prefix"
 test $exec_prefix = NONE || pkg_prefix="$pkg_prefix --define-variable=exec_prefix=$exec_prefix"
 
-PKG_CHECK_MODULES(GIMP, gimpui-2.0,
-  [ have_gimp=yes
-    GIMP_LIBDIR=`$pkg_prefix --variable=gimplibdir gimp-2.0` ],
-  [ have_gimp=no
-    GIMP_LIBDIR=
-    AC_MSG_RESULT($GIMP_PKG_ERRORS) ] )
+AC_ARG_WITH([gimp],
+  [AS_HELP_STRING([--with-gimp],
+  [build gimp plugin @<:@default=check@:>@])],
+  [],
+  [with_gimp=check])
+
+have_gimp=no
+AS_IF([test "x$with_gimp" != xno],
+  [PKG_CHECK_MODULES(GIMP, gimpui-2.0,
+    [ have_gimp=yes
+      GIMP_LIBDIR=`$pkg_prefix --variable=gimplibdir gimp-2.0` ],
+    [ have_gimp=no
+      GIMP_LIBDIR=
+      if test "x$with_gimp" != xcheck; then
+        AC_MSG_FAILURE(
+          [--with-gimp was given, but test for gimp failed])
+      fi
+    ] ) ] )
+
 AM_CONDITIONAL(MAKE_GIMP, test $have_gimp = yes)
 AC_SUBST(GIMP_CFLAGS)
 AC_SUBST(GIMP_LIBS)
@@ -122,14 +149,27 @@
     [ AC_DEFINE_UNQUOTED(HAVE_GIMP_2_6, 0, have Gimp 2.6 or later) ])
 fi
 
-PKG_CHECK_MODULES(CINEPAINT, cinepaint-gtk >= 0.22,
-  [ have_cinepaint=yes
-    CINEPAINT_LIBDIR=`$pkg_prefix --variable=libdir cinepaint-gtk`
-    CINEPAINT_PROGRAMPLUGINDIR=`$pkg_prefix --variable=programplugindir cinepaint-gtk` ],
-  [ have_cinepaint=no
-    CINEPAINT_LIBDIR=
-    CINEPAINT_PROGRAMPLUGINDIR=
-    AC_MSG_RESULT($CINEPAINT_PKG_ERRORS) ] )
+AC_ARG_WITH([cinepaint],
+  [AS_HELP_STRING([--with-cinepaint],
+  [build cinepaint plugin @<:@default=check@:>@])],
+  [],
+  [with_cinepaint=check])
+
+have_cinepaint=no
+AS_IF([test "x$with_cinepaint" != xno],
+  [PKG_CHECK_MODULES(CINEPAINT, cinepaint-gtk >= 0.22,
+    [ have_cinepaint=yes
+      CINEPAINT_LIBDIR=`$pkg_prefix --variable=libdir cinepaint-gtk`
+      CINEPAINT_PROGRAMPLUGINDIR=`$pkg_prefix --variable=programplugindir cinepaint-gtk` ],
+    [ have_cinepaint=no
+      CINEPAINT_LIBDIR=
+      CINEPAINT_PROGRAMPLUGINDIR=
+      if test "x$with_cinepaint" != xcheck; then
+        AC_MSG_FAILURE(
+          [--with-cinepaint was given, but test for cinepaint failed])
+      fi
+    ] ) ] )
+
 AM_CONDITIONAL(MAKE_CINEPAINT, test $have_cinepaint = yes)
 AC_SUBST(CINEPAINT_CFLAGS)
 AC_SUBST(CINEPAINT_LIBS)
@@ -189,23 +229,43 @@
   AC_CHECK_LIB(cfitsio, ffcmsg))
 have_fits=${ac_cv_lib_cfitsio_ffcmsg:-no}
 
-PKG_CHECK_MODULES(EXIV2, exiv2 >= 0.11,
-  [ have_exiv2=yes
-    AC_DEFINE(HAVE_EXIV2, 1, have exiv2) ],
-  [ have_exiv2=no
-    AC_MSG_RESULT($EXIV2_PKG_ERRORS) ] )
+# check for exiv2 library
+AC_ARG_WITH([exiv2],
+  [AS_HELP_STRING([--with-exiv2],
+  [use exiv2 library for exif information extraction @<:@default=check@:>@])],
+  [],
+  [with_exiv2=check])
+
+have_exiv2=no
+AS_IF([test "x$with_exiv2" != xno],
+  [PKG_CHECK_MODULES(EXIV2, exiv2 >= 0.11,
+    [ have_exiv2=yes
+      AC_DEFINE(HAVE_EXIV2, 1, have exiv2) ],
+    [ have_exiv2=no
+      if test "x$with_exiv2" != xcheck; then
+        AC_MSG_FAILURE(
+          [--with-exiv2 was given, but test for exiv2 failed])
+      fi
+    ] ) ] )
 
 # Check for the lensfun library
-AC_MSG_CHECKING(whether to use lensfun)
-AC_ARG_WITH(lensfun,
-  [  --with-lensfun          use the lensfun library - experimental feature,
-              read http://ufraw.sourceforge.net/lensfun.html before using it. ],
-  [ AC_MSG_RESULT(yes)
-    PKG_CHECK_MODULES(LENSFUN, lensfun >= 0.2.3,
-      [ have_lensfun=yes
-        AC_DEFINE(HAVE_LENSFUN, 1, have lensfun library) ] ) ],
-  [ have_lensfun=no
-    AC_MSG_RESULT($have_lensfun) ] )
+AC_ARG_WITH([lensfun],
+  [AS_HELP_STRING([--with-lensfun],
+  [use the lensfun library - experimental feature, read http://ufraw.sourceforge.net/lensfun.html before using it. @<:@default=check@:>@])],
+  [],
+  [with_lensfun=check])
+
+have_lensfun=no
+AS_IF([test "x$with_lensfun" != xno],
+  [PKG_CHECK_MODULES(LENSFUN, lensfun >= 0.2.3,
+    [ have_lensfun=yes
+      AC_DEFINE(HAVE_LENSFUN, 1, have lensfun library) ],
+    [ have_lensfun=no
+      if test "x$with_lensfun" != xcheck; then
+        AC_MSG_FAILURE(
+          [--with-lensfun was given, but test for lensfun failed])
+      fi
+    ] ) ] )
 
 # UFRAW_CPPFLAGS is added to the preprocessor flags AM_CPPFLAGS,
 # affecting also the C and C++ compilers.
